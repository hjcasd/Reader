apply plugin: 'com.android.application'

//获取当前日期
def static releaseTime() {
    return new Date().format("yyyy-MM-dd")
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.hjc.reader"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 2
        versionName "2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        ndk {
            // 设置支持的SO库架构(必须设置如下几种,否则X5会初始化失败)
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }


    buildTypes {
        release {
            //是否混淆
            minifyEnabled false
            //是否移除无用资源
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'BASE_URL', '"https://yinbao.enjoyqb.com:15050/ybapi/admin-web/services/"'
            buildConfigField 'boolean', 'IS_DEBUG', "false"
        }

        debug {
            //是否混淆
            minifyEnabled false
            //是否移除无用资源
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'BASE_URL', '"https://yinbao.enjoyqb.com:15050/ybapi/admin-web/services/"'
            buildConfigField 'boolean', 'IS_DEBUG', "true"
        }
    }

    //指定打包路径和apk名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName = "Reader_V${variant.versionName}"
            fileName = fileName + "_${releaseTime()}"
            if ('debug' == variant.buildType.name) {
                fileName = fileName + "_debug.apk"
            } else {
                fileName = fileName + "_release.apk"
            }
            outputFileName = fileName
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    buildFeatures {
        dataBinding = true
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'

    //Room
    implementation "androidx.room:room-runtime:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"

    //分包处理
    implementation 'androidx.multidex:multidex:2.0.1'

    //图片处理框架
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    //路由(当前项目需引入该依赖)
    annotationProcessor 'com.alibaba:arouter-compiler:1.5.1'

    //Json
    implementation 'com.google.code.gson:gson:2.8.6'

    //列表
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.6'

    //刷新
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.2'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.2'

    //Banner
    implementation 'com.youth.banner:banner:2.1.0'

    //Bugly
    implementation 'com.tencent.bugly:crashreport:3.2.1'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'

    //流式布局
    implementation 'com.nex3z:flow-layout:1.2.4'

    // 图片浏览
    implementation 'com.github.Hitomis.transferee:Transferee:1.6.1'
    implementation 'com.github.Hitomis.transferee:GlideImageLoader:1.6.1'

    //Zxing二维码
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'

    implementation project(path: ':BaseLib')
    implementation project(path: ':WebViewLib')
}